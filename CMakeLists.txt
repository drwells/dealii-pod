CMAKE_MINIMUM_REQUIRED(VERSION 2.8.8)

PROJECT(deal.II-pod CXX)

FIND_PACKAGE(deal.II 8.4 QUIET
  HINTS ${deal.II_DIR} ${DEAL_II_DIR} ../ ../../ $ENV{DEAL_II_DIR})

IF(NOT ${deal.II_FOUND})
  MESSAGE(FATAL_ERROR "\n"
    "*** Could not locate deal.II. ***\n\n"
    "You may want to either pass a flag -DDEAL_II_DIR=/path/to/deal.II to cmake\n"
    "or set an environment variable \"DEAL_II_DIR\" that contains this path."
    )
ENDIF()
DEAL_II_INITIALIZE_CACHED_VARIABLES()

FIND_PACKAGE(OpenMP)
IF(OPENMP_FOUND)
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
ENDIF()

# Modify the warning flags to be saner (more messages, less template barf)
INCLUDE(CheckCXXCompilerFlag)
SET(DEAL_II_ROM_TEMPLATE_ERROR_FLAGS "-fmax-errors=3 -ftemplate-backtrace-limit=1")
CHECK_CXX_COMPILER_FLAG(DEAL_II_ROM_TEMPLATE_ERROR_FLAGS
  DEAL_II_ROM_HAVE_TEMPLATE_ERROR_FLAGS)
IF(DEAL_II_ROM_HAVE_TEMPLATE_ERROR_FLAGS)
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${DEAL_II_ROM_TEMPLATE_ERROR_FLAGS}")
ENDIF()

SET(DEAL_II_ROM_WARNING_FLAGS "-Wextra -pedantic -Wall")
CHECK_CXX_COMPILER_FLAG(DEAL_II_ROM_WARNING_FLAGS DEAL_II_ROM_HAVE_WARNING_FLAGS)
IF(DEAL_II_ROM_HAVE_WARNING_FLAGS)
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${DEAL_II_ROM_WARNING_FLAGS}")
ENDIF()

INCLUDE_DIRECTORIES("include")

# define executables here so that the test suite can find them
SET(NS_ROM_TARGET ns-rom)

ADD_SUBDIRECTORY("source")
ADD_SUBDIRECTORY("programs")

ADD_CUSTOM_TARGET(debug
  COMMAND ${CMAKE_COMMAND} -DCMAKE_BUILD_TYPE=Debug ${CMAKE_SOURCE_DIR}
  COMMAND ${CMAKE_COMMAND} --build ${CMAKE_BINARY_DIR} --target all
  COMMENT "Switch CMAKE_BUILD_TYPE to Debug"
  )

ADD_CUSTOM_TARGET(release
  COMMAND ${CMAKE_COMMAND} -DCMAKE_BUILD_TYPE=Release ${CMAKE_SOURCE_DIR}
  COMMAND ${CMAKE_COMMAND} --build ${CMAKE_BINARY_DIR} --target all
  COMMENT "Switch CMAKE_BUILD_TYPE to Release"
  )
